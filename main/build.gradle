plugins {
    id("java")
    id("org.jetbrains.kotlin.jvm")
    id("application")
}

group = "org.luckypray"
version = "1.0-SNAPSHOT"

def library = "${layout.buildDirectory.file("library").get().asFile}"
def args = ["-Djava.library.path=$library"]

application {
    applicationDefaultJvmArgs += args
    mainClass.set("MainKt")
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    toolchain.languageVersion.set(JavaLanguageVersion.of(11))
}

dependencies {
    implementation(project(":dexkit"))
}


afterEvaluate {
    tasks.register("cmakeBuild") {
        group = "build"
        def build = project(":dexkit").tasks.named("cmakeBuild")
        dependsOn(build)
        doLast {
            copy {
                from project(":dexkit").layout.buildDirectory.file("cmake").get()
                into library
                include("**/*.so", "**/*.dll", "**/*.dylib")
                eachFile {
                    path = name
                }
                includeEmptyDirs = false
            }
        }
    }
    tasks.jar.dependsOn(tasks.cmakeBuild)
    tasks.run.dependsOn(tasks.jar)
}

evaluationDependsOn(":dexkit")
