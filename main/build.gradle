plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id 'application'
}

group = 'org.luckypray'
version = '1.0-SNAPSHOT'

def library = "$buildDir/library"
def args = ["-Djava.library.path=$library"]

application {
    applicationDefaultJvmArgs += args
    mainClass.set("MainKt")
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    implementation project(':dexkit-lib')
}


afterEvaluate {
    tasks.register('cmakeBuild') {
        group = 'build'
        def build = project(':dexkit-lib').tasks.named('cmakeBuild')
        dependsOn(build)
        doLast {
            copy {
                from("${project(':dexkit-lib').buildDir}/cmake")
                into library
                include('**/*.so', '**/*.dll', '**/*.dylib')
                eachFile {
                    path = name
                }
                includeEmptyDirs = false
            }
        }
    }
    tasks.jar.dependsOn(tasks.cmakeBuild)
    tasks.run.dependsOn(tasks.jar)
}

evaluationDependsOn(":dexkit-lib")
